// KQL samples - Long Running SQL Queries

// Important: if you query telemetry from an environment on version 15.0-15.2 
// Then please see below for a sample query. The schema changed in 15.3 

// Long running SQL queries (RAW data)
// Use extend if you need to use a column extracted from customDimensions in a function (such as countof)
// This example uses project-away to filter out all "internal" columns
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == 'Long Running Operation (SQL Query)' // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
// | where customDimensions.eventCode == 'TODO - coming in a later version of the schema'
| where customDimensions.alObjectId > 0 // filter out internal server calls
| extend aadID = customDimensions.aadTenantId
, sqlStatement = tostring(customDimensions.sqlStatement)
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, alStackTrace = customDimensions.alStackTrace
, clientType = customDimensions.clientType
, executionTime = customDimensions.executionTime
, executionTimeInMS = toreal(totimespan(customDimensions.executionTime))/10000 //the datatype for executionTime is timespan 
, extensionId = customDimensions.extensionId
, extensionName = customDimensions.extensionName
, extensionVersion = customDimensions.extensionVersion
| extend numberOfJoins = countof(sqlStatement, "JOIN") 
, numberOfOuterApplys = countof(sqlStatement, "OUTER APPLY") 
| project-rename environmentName=cloud_RoleInstance 
| project-away severityLevel, itemType, customMeasurements, client_Browser, client_City, client_CountryOrRegion, client_IP, client_OS, client_Type, client_StateOrProvince, client_Model , operation_SyntheticSource, operation_ParentId, user_Id, user_AuthenticatedId, user_AccountId , application_Version, sdkVersion, iKey, appId, appName, itemId, itemCount, operation_Name, operation_Id 


// Long running SQL queries (RAW data)
// Use renames in the project operator if you don't need to filter on the columns extracted from customDimensions
// This example uses project to only include the columns needed in the result set
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == 'Long Running Operation (SQL Query)' // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
// | where customDimensions.eventCode == 'TODO - coming in a later version of the schema'
| where customDimensions.alObjectId > 0 // filter out internal server calls
| extend aadID = customDimensions.aadTenantId
, sqlStatement = tostring(customDimensions.sqlStatement)
|extend numberOfJoins = countof(sqlStatement, "JOIN") 
, numberOfOuterApplys = countof(sqlStatement, "OUTER APPLY") 
| project aadID
, environmentName=cloud_RoleInstance 
, environmentType = customDimensions.environmentType
, companyName = customDimensions.companyName
, alObjectId = customDimensions.alObjectId
, alObjectName = customDimensions.alObjectName
, alObjectType = customDimensions.alObjectType
, alStackTrace = customDimensions.alStackTrace
, clientType = customDimensions.clientType
, executionTime = customDimensions.executionTime
, executionTimeInMS = toreal(totimespan(customDimensions.executionTime))/10000 //the datatype for executionTime is timespan 
, extensionId = customDimensions.extensionId
, extensionName = customDimensions.extensionName
, extensionVersion = customDimensions.extensionVersion
, sqlStatement 
, numberOfJoins 
, numberOfOuterApplys 
, message, customDimensions,  


// version that works on 15.0, 15.2, and 15.2 versions
traces
| where timestamp > ago(60d) // adjust as needed
| where operation_Name == 'Long Running Operation (SQL Query)' // do note that in a later version of the schema, this field will not be used (a new field in custom dimensions will be used)
| extend aadID = customDimensions.['AadTenantId']
, executionTime = customDimensions.['Execution time (ms)']
, sqlStatement = tostring( customDimensions.['SQL statement'] )
| extend alObjectId = tostring( customDimensions.['AL Object Id'] )
, alObjectName = tostring( customDimensions.['AL Object name'] )
, alObjectType = tostring( customDimensions.['AL Object type'] )
, alStackTrace = tostring( customDimensions.['AL Stack trace'] )
, clientType = tostring( customDimensions.['Client type'] )
, companyName = tostring( customDimensions.['Company name'] )
, component = customDimensions.['Component']
, componentVersion = customDimensions.['Component version']
, environmentName = tostring( customDimensions.['Environment name'] )
, environmentType = tostring( customDimensions.['Environment type'] )
, executionTimeInMS = toreal(totimespan(executionTime))/10000 //the datatype for executionTime is timespan 
, numberOfJoins = countof(sqlStatement, "JOIN") 
, numberOfOuterApplys = countof(sqlStatement, "OUTER APPLY") 